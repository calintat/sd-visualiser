program = _{ SOI ~ expr ~ EOI }

keyword = { "bind" | "in" }

expr = { bind_clause* ~ value }
bind_clause = { "bind" ~ variable ~ "=" ~ value ~ "in" }

thunk = { variable* ~ "." ~ expr }

value = { variable | op ~ ("(" ~ (thunks | values ~ (";" ~ thunks)?) ~ ")")? }
thunks = _{ thunk ~ ("," ~ thunk)* }
values = _{ value ~ ("," ~ value)* }

op = { "plus" | "minus" | "times" | "eq" | "and" | "or" | "not" | "if" | "app" | "rec" | number | "true" | "false" | "lambda" }
number = @{ ASCII_DIGIT+ }

variable = @{ !(keyword | op) ~ "_"* ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\n" }