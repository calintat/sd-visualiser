program = _{ SOI ~ expr ~ EOI }

keyword = { "bind" | "in" }

expr = { bind* ~ output }
bind = { "bind" ~ variable ~ "=" ~ value ~ "in" }
output = _{ value | "(" ~ (value ~ ("," ~ value)+)? ~ ")" }

thunk = { variable* ~ "." ~ expr }

value = { variable | op ~ ("(" ~ args ~ ")")? }
arg = _{ thunk | value }
args = _{ arg ~ ("," ~ args)* }

op = { "plus" | "minus" | "times" | "div" | "rem" | "and" | "or" | "not" | "if" | "eq" | "neq" | "lt" | "leq" | "gt" | "geq" | "app" | "lambda" | "atom" | "deref" | "assign" | "true" | "false" | number }
number = @{ ASCII_DIGIT+ }

variable = @{ !(keyword | op) ~ "_"* ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\n" }