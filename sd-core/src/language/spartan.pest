program = _{ SOI ~ expr ~ EOI }

keyword = { "bind" | "in" }

expr = { bind* ~ values? }
bind = { "bind" ~ variable ~ "=" ~ value ~ "in" }

thunk = { "\\" ~ variable* ~ "." ~ expr }

value = { variable | op ~ ("(" ~ args ~ ")")? }
arg = _{ value | thunk }
args = _{ arg ~ ("," ~ args)* }
values = _{ value ~ ("," ~ value)* }

op = { "plus" | "minus" | "times" | "div" | "rem" | "and" | "or" | "not" | "if" | "eq" | "neq" | "lt" | "leq" | "gt" | "geq" | "app" | "lambda" | "atom" | "deref" | "assign" | "true" | "false" | number }
number = @{ ASCII_DIGIT+ }

variable = @{ !(keyword | op) ~ "_"* ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\n" }