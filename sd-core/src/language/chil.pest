program = _{ SOI ~ expr ~ EOI }

keyword = { "def" | "output" | "thunk" }

expr = { bind_clause* ~ "output" ~ value }
bind_clause = { "def" ~ variable_def ~ "=" ~ value }

thunk = { "thunk" ~ identifier ~ "=" ~ "{" ~ thunk_args ~ "=>" ~ expr ~ "}" }
thunk_args = _{ variable_def ~ ("," ~ variable_def)* | "" }

value = { variable | op ~ ("(" ~ args ~ ")")? }

arg = { thunk | value }
args = _{ arg ~ (("," | ";") ~ arg)* | "" }

op = @{ !keyword ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "+" | "-" | "*" | "=")* ~ ("/" ~ (number | string | ty ~ "/" ~ ty))? }
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string = !{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

ty = { atomic_ty | function_ty }
atomic_ty = @{ 'A'..'Z' ~ (ASCII_ALPHANUMERIC | "-")* }
function_ty = !{ "(" ~ tys ~ ")" ~ "->" ~ atomic_ty }
tys = _{ ty ~ ("," ~ ty)* | "" }
variable = { identifier | name ~ "(" ~ "id" ~ ":" ~ identifier ~ ")" }
variable_def = { variable ~ ":" ~ ty | variable }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\n" }

identifier = @{ ("@" | "%") ~ number }
name = @{ !keyword ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }