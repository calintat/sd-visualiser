program = _{ SOI ~ expr ~ EOI }

keyword = { "def" | "output" | "thunk" }

expr = { bind_clause* ~ "output" ~ value }
bind_clause = { "def" ~ variable_def ~ "=" ~ value }

thunk = { "thunk" ~ addr ~ "=" ~ "{" ~ (variable_def ~ ("," ~ variable_def)*)? ~ "=>" ~ expr ~ "}" }

value = { variable | op ~ ("(" ~ (thunk+ | values ~ (";" ~ thunk+)?) ~ ")")? }
values = _{ value ~ ("," ~ value)* }

op = ${ !keyword ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "+" | "-" | "*" | "=")* ~ ("/" ~ (number | string | ty ~ "/" ~ ty))? }
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

ty = { generic_ty | base_ty | function_ty | tuple_ty }
tys = _{ ty ~ ("," ~ ty)* }
base_ty = @{ 'A'..'Z' ~ (ASCII_ALPHANUMERIC | "-")* }
generic_ty = { base_ty ~ "<" ~ tys ~ ">" }
tuple_ty = { "(" ~ tys? ~ ")" }
function_ty = { tuple_ty ~ "->" ~ ty }

variable = { addr | identifier ~ "(" ~ "id" ~ ":" ~ addr ~ ")" }
variable_def = { variable ~ ":" ~ ty | variable }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\n" }

addr = @{ ("@" | "%") ~ ASCII_DIGIT+ }
identifier = @{ !keyword ~ "_"* ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }